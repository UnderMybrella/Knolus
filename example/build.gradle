plugins {
    id 'java'
    id 'antlr'
    id 'org.jetbrains.kotlin.jvm'
    id "de.undercouch.download"
}

apply plugin: 'maven-publish'

group 'dev.brella'
version '1.2.2'

sourceCompatibility = 8

repositories {
    maven { url 'https://kotlin.bintray.com/kotlinx' }
    maven { url "https://maven.brella.dev" }
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.1"
    antlr "org.antlr:antlr4:4.7.2"

    implementation project(":knolus-core")

//    implementation "dev.brella:kornea-errors:2.0.2"
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    baseName = jar.baseName
    appendix = jar.appendix
    version = jar.version
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    baseName = jar.baseName
    appendix = jar.appendix
    version = jar.version
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    repositories {
        maven {
            // change to point to your repo, e.g. http://my.org/repo
            url = "${rootProject.buildDir}/repo"
        }
    }

    publications {
            maven(MavenPublication) {
//                artifactId = "spiral-$project.name"
                from components.java
                artifact sourcesJar
                artifact javadocJar
            }
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
    kotlinOptions {
        freeCompilerArgs = ["-Xinline-classes", "-Xopt-in=kotlin.RequiresOptIn"]
    }
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

generateGrammarSource {
    arguments += ['-visitor', '-package', 'dev.brella.antlr.knolus']
    outputDirectory = file("$buildDir/generated-src/antlr/main/dev/brella/antlr/knolus")
}

task copyKnolus(type: Copy) {
    from "$rootDir/core/src/main/antlr"
    into "$projectDir/src/main/antlr"
    include "*.g4"
}

tasks.compileKotlin.dependsOn(generateGrammarSource)
tasks.generateGrammarSource.dependsOn(copyKnolus)